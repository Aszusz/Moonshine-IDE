<?xml version="1.0" encoding="utf-8"?>
<resizableTitleWindow:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 title="Replace Text Matches"
		 skinClass="components.skins.ResizableTitleWindowSkin" xmlns:resizableTitleWindow="actionScripts.ui.resizableTitleWindow.*" xmlns:marker="actionScripts.ui.marker.*"
		 initialize="onInit(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import spark.events.IndexChangeEvent;
			
			import actionScripts.events.GeneralEvent;
			import actionScripts.events.WorkerEvent;
			import actionScripts.factory.FileLocation;
			import actionScripts.locator.IDEWorker;
			import actionScripts.utils.TextUtil;
			
			[Bindable] public var filesCount:int;
			
			public var matchCount:int;
			public var replaceValue:String;
			public var replaceWithValue:String;
			public var isMatchCase:Boolean;
			public var isRegexp:Boolean;
			public var isEscapeChars:Boolean;
			
			[Bindable] private var storedPathsForProbableReplace:ArrayCollection;
			
			private var worker:IDEWorker = IDEWorker.getInstance();
			
			override protected function closeByCrossSign(event:Event):void
			{
				worker.removeEventListener(IDEWorker.WORKER_VALUE_INCOMING, onWorkerValueIncoming);
				super.closeByCrossSign(event);
			}
			
			protected function onInit(event:FlexEvent):void
			{
				worker.addEventListener(IDEWorker.WORKER_VALUE_INCOMING, onWorkerValueIncoming, false, 0, true);
				worker.sendToWorker(WorkerEvent.GET_FILE_LIST, null);
			}
			
			private function onWorkerValueIncoming(event:GeneralEvent):void
			{
				switch (event.value.event)
				{
					case WorkerEvent.GET_FILE_LIST:
						storedPathsForProbableReplace = new ArrayCollection(event.value.value);
						generateUI();
						break;
				}
			}
			
			protected function generateUI():void
			{
				if (!storedPathsForProbableReplace || storedPathsForProbableReplace.length == 0) return;
				
				marker1.isEscapeChars = marker2.isEscapeChars = isEscapeChars;
				marker1.isMatchCase = marker2.isMatchCase = isMatchCase;
				marker1.isRegexp = marker2.isRegexp = isRegexp;
				
				// pre-load the first instance from the list
				updateMarkers(storedPathsForProbableReplace[0].label);
			}
			
			protected function onListSelectionChanged(event:IndexChangeEvent):void
			{
				updateMarkers(lstFiles.selectedItem.label);
			}
			
			private function updateMarkers(filePath:String):void
			{
				var searchString:String = isEscapeChars ? TextUtil.escapeRegex(replaceValue) : replaceValue;
				var flags:String = 'g';
				if (!isMatchCase) flags += 'i';
				var searchRegExp:RegExp = new RegExp(searchString, flags);
				
				var tmpContent:String = new FileLocation(filePath).fileBridge.read() as String;
				marker1.text = tmpContent;
				marker1.searchRegExp = searchRegExp;
				marker1.highlight(replaceValue);
				
				marker2.text = tmpContent;
				marker2.searchRegExp = searchRegExp;
				marker2.highlight(replaceWithValue);
			}
			
			protected function onReplaceSelectedFiles(event:MouseEvent):void
			{
				var selectedFilesCount:int = getSelectedFilesCount();
				if (selectedFilesCount == 0) return;
							
				// update file list array in worker 
				worker.sendToWorker(WorkerEvent.SET_FILE_LIST, storedPathsForProbableReplace.source);
				
				// open progress smaller window
				var replacePopup:ReplaceProjectSearchPopup = PopUpManager.createPopUp(FlexGlobals.topLevelApplication as DisplayObject, ReplaceProjectSearchPopup, true) as ReplaceProjectSearchPopup;
				replacePopup.replaceValue = replaceValue;
				replacePopup.replaceWithValue = replaceWithValue;
				replacePopup.matchCount = matchCount;
				replacePopup.filesCount = selectedFilesCount;
				replacePopup.isMatchCase = isMatchCase;
				replacePopup.isRegexp = isRegexp;
				replacePopup.isEscapeChars = isEscapeChars;
				replacePopup.isStartReplaceImmediately = true;
				
				PopUpManager.centerPopUp(replacePopup);
				closeThis();
			}
			
			private function getSelectedFilesCount():int
			{
				var tmp:int;
				for each (var i:Object in storedPathsForProbableReplace)
				{
					if (i.isSelected) tmp++;
				}
				
				return tmp;
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup height="100%" width="100%" 
			  gap="10" paddingLeft="12" paddingBottom="3" paddingTop="9" paddingRight="13">
		<s:Label text="Changes can be performed in {filesCount} files:"/>
		<s:List id="lstFiles"
				width="100%" height="{this.height*.2}" styleName="multiLineList"
				dataProvider="{storedPathsForProbableReplace}" change="onListSelectionChanged(event)"
				requireSelection="true" labelField="label">
			<s:itemRenderer>
				<fx:Component>
					<s:ItemRenderer width="100%" height="22">
						<fx:Script>
							<![CDATA[
								override public function set data(value:Object):void
								{
									if (value) super.data = value;
								}
								
							]]>
						</fx:Script>
						
						<s:layout>
							<s:HorizontalLayout verticalAlign="middle" paddingLeft="4" paddingRight="6" gap="10"/>
						</s:layout>
						
						<s:CheckBox id="cbSelect"
									change="data.isSelected = cbSelect.selected" selected="{data.isSelected}"/>
						<s:Label id="labelDisplay"/>
					</s:ItemRenderer>
				</fx:Component>
			</s:itemRenderer>
		</s:List>
		<s:HGroup width="100%">
			<s:Label text="Original source:"
					 width="50%"/>
			<s:Label text="Refactored source:"/>
		</s:HGroup>
		<s:HGroup width="100%" height="100%">
			<marker:MarkerTextArea id="marker1"
								   width="50%" height="100%"
								   VSCrollUpdate="{marker2.updateVScrollByNeighbour(event)}" HSCrollUpdate="{marker2.updateHScrollByNeighbour(event)}"/>
			<marker:MarkerTextArea id="marker2"
								   width="50%" height="100%"
								   isRefactoredView="true"
								   VSCrollUpdate="{marker1.updateVScrollByNeighbour(event)}" HSCrollUpdate="{marker1.updateHScrollByNeighbour(event)}"/>
		</s:HGroup>
	</s:VGroup>
	
	<resizableTitleWindow:controlBarContent>
		<s:Button id="btnReplace" label="Replace All Selected"
				  styleName="darkButton"
				  click="onReplaceSelectedFiles(event)"/>
	</resizableTitleWindow:controlBarContent>
</resizableTitleWindow:ResizableTitleWindow>
